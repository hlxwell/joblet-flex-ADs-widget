<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
	layout="absolute" 
	initialize="onLoad(event)"
	xmlns:local="*"
    backgroundColor="#ffffff"
    backgroundGradientColors="#ffffff, #ffffff"
	borderThickness="1" 
	borderColor="#000000"
	borderStyle="solid"
    verticalScrollPolicy="off" horizontalScrollPolicy="off">

    <mx:Style source="style.css"/>
	<mx:Script>
		<![CDATA[
			import mx.controls.listClasses.TileBase;
			import mx.events.ListEvent;
			import mx.controls.Alert;
			import mx.collections.ArrayCollection;
			import mx.rpc.events.ResultEvent;
			
			[Bindable]
            private var jobs:ArrayCollection = new ArrayCollection();

			private var jobletHomeURL:String = "";
			private var FrameColor:String = "#000000";
			private var TitleWordColor:String = "#ffffff";
			private var ContentBgColor:String = "#ffffff";
			private var ContentWordColor:String = "#000000";
			private var miniWidth:int = 280;
			private var wordWidth:int = 200;

			private function onLoad(event:Event):void {
				setColor();
				var params:Object = new Object();
                params.limit = '100';
                jobService.url = Application.application.parameters.service_url;
                jobService.send(params);
			}

			private function setColor():void {
				var params:Object = Application.application.parameters;
				
				FrameColor = params.frame_color;
				TitleWordColor = params.title_color;
				ContentBgColor = params.content_bg_color;
				ContentWordColor = params.content_color;
				
				titleFrame.setStyle("backgroundColor", FrameColor);
				title.setStyle("color", TitleWordColor);
				jobList.setStyle("backgroundColor", ContentBgColor);
				footFrame.setStyle("backgroundColor", ContentBgColor);
				footFrame.setStyle("color", ContentWordColor);
				this.setStyle("borderColor", FrameColor);
			}

			private function onDataResponse(event:ResultEvent):void {
				var new_jobs:ArrayCollection = new ArrayCollection();
	
				// set home link and title words
				jobletHomeURL = event.result.jobs.link_to_home;
				title.text = event.result.jobs.job_board_name;

				if(stage != null) {
					if(stage.width > miniWidth && stage.width < 560) {
						jobList.columnWidth = stage.width - 5;
						jobList.columnCount = 1;

						wordWidth = stage.width*0.70;
					} else if(stage.width >= 560 && stage.width < 840) {
						jobList.columnWidth = stage.width/2 - 5;
						jobList.columnCount = 2;

						wordWidth = (stage.width/2)*0.70;
					} else if(stage.width <= miniWidth) {						
						jobList.columnWidth = miniWidth - 5;
						jobList.columnCount = 1;

						wordWidth = stage.width*0.70;
					} else if(stage.width >= 840) {
						jobList.columnWidth = stage.width/3 - 5;
						jobList.columnCount = 3;

						wordWidth = (stage.width/3)*0.70;
					}
				}

				// add property to job object
				for each (var j:Object in event.result.jobs.job as ArrayCollection) {
					j.wordColor = ContentWordColor;
					j.miniWidth = miniWidth;
					j.wordWidth = wordWidth;
					new_jobs.addItem(j);
				}

				jobs = new_jobs;

				trace(stage.width.toString() + "," + jobList.columnWidth.toString() + "," + jobList.columnCount.toString());
			}

			private function pageUp(event:MouseEvent):void {
				if(jobList.verticalScrollPosition > 0){
					jobList.verticalScrollPosition -= 1; //-= jobList.rowCount;
				}
			}

			private function pageDown(event:MouseEvent):void {
				if(jobList.maxVerticalScrollPosition > jobList.verticalScrollPosition){
					jobList.verticalScrollPosition += 1; //+= jobList.rowCount;
				}
			}

			private function gotoJobletHome(event:MouseEvent):void {
				if(jobletHomeURL != "") {
					navigateToURL( new URLRequest( jobletHomeURL ), "_blank" );
				}
			}
		]]>
	</mx:Script>

	<mx:HTTPService id="jobService" result="onDataResponse(event)" />

	<mx:VBox id="frame" 
		width="100%" height="100%" 
		left="0" top="0"
		verticalGap="0">

		<mx:VBox id="titleFrame" width="100%" verticalAlign="middle" height="30">
			<mx:Label id="title" text="" styleName="title" paddingLeft="10" paddingTop="2"/>
		</mx:VBox>

		<mx:TileList id="jobList"
			dataProvider="{jobs}" selectable="false"
			width="100%" height="100%"
		    columnWidth="280"
		    borderThickness="0"
		    horizontalScrollPolicy="off"
		    verticalScrollPolicy="off">
			<mx:itemRenderer>
				<mx:Component>
					<local:widget_item job="{data}"/>
	            </mx:Component>
			</mx:itemRenderer>
		</mx:TileList>

		<mx:VBox id="footFrame" width="100%" verticalAlign="bottom">
			<mx:LinkButton id="linkToHome"
				label="See more jobs on AMN Job Board"
				styleName="htmlLink1"
			    paddingLeft="5"
			    paddingTop="5"
			    click="gotoJobletHome(event)"
				rollOver="linkToHome.styleName='htmlLinkHover1';"
		        rollOut="linkToHome.styleName='htmlLink1';"/>
	    </mx:VBox>
	</mx:VBox>

	<!-- page button -->
	<mx:LinkButton label="^" top="31" right="1" fontSize="14" click="pageUp(event)"/>
	<mx:LinkButton label="v" bottom="1" right="1" fontSize="14" click="pageDown(event)"/>
</mx:Application>